plugins {
	id 'java'
	id 'jacoco'
	id 'jacoco-report-aggregation'
	id 'org.springframework.boot' version '3.1.5'
	id 'io.spring.dependency-management' version '1.1.3'
	id "com.github.node-gradle.node" version "7.0.1"
}

group = 'com.mkr'
version = '0.0.1-SNAPSHOT'

java {
	sourceCompatibility = '17'
}

repositories {
	mavenCentral()
}

dependencies {
	implementation project(":datastore")
	jacocoAggregation project(':datastore')

	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.jetbrains:annotations:24.0.0'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

tasks.named('bootBuildImage') {
	builder = 'paketobuildpacks/builder-jammy-base:latest'
}

tasks.named('test') {
	useJUnitPlatform()
	finalizedBy jacocoTestReport
}

processResources {
	with copySpec {
		from "${buildDir}/fe"
		include "**/*.html"
		into "public"
	}

	with copySpec {
		from "${buildDir}/fe"
		include "**/*.js", "**/*.css"
		into "static"
	}
}

node {
	nodeProjectDir = project.rootProject.file("frontend")
}

tasks.register('webpack', NodeTask) {
	dependsOn 'npmInstall'
	script = project.rootProject.file('frontend/node_modules/webpack/bin/webpack.js')
	args = ["--env=output=${buildDir}/fe", "--env=springBuild"]
}

processResources.dependsOn 'webpack'

reporting {
	reports {
		testCodeCoverageReport(JacocoCoverageReport) {
			testType = TestSuiteType.UNIT_TEST
		}
	}
}

jacocoTestReport {
	dependsOn test
	dependsOn(":datastore:test")

	reports {
		xml.required = false
		csv.required = false
		html.outputLocation = layout.buildDirectory.dir('jacocoHtml')
	}
}